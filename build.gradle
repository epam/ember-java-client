plugins {
    // Supports functionality similar to Maven BOM.
    // Helps to avoid re-declaring dependency version in each subproject.
    // See https://github.com/spring-gradle-plugins/dependency-management-plugin
    id "io.spring.dependency-management" version "1.1.5" apply true
    id 'java'
    id 'application'
    id 'idea'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'


apply from: "$rootDir/gradle/deltix.gradle"

repositories {
    mavenCentral()
    deltixMavenRepository("epm-rtc-public-java")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    emberVersion = '1.14.185'
}

dependencyManagement {
    imports {
        mavenBom 'deltix:deltix-ember-bom:' + emberVersion
    }
}


dependencies {
    // for "slim" samples in deltix.ember.message.bus.samples.*
    implementation "deltix:deltix-ember-message-bus-client"

    // for "fat" samples in deltix.ember.sample.* that Access EMBER_HOME etc
    implementation "deltix:deltix-ember-app:$emberVersion" // not a part of Ember BOM

    // @Nullable, etc.
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}

application {
    mainClassName = "deltix.ember.message.bus.samples.DuplexSample"
}

